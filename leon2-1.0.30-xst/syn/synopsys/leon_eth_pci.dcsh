/*******************************************/
/* Script to compile leon with synopsys DC */
/* Jiri Gaisler, Gaisler Research, 2001    */
/*******************************************/

/* include paths to your target and link libraries below. */

/* include atc25setup.dcsh */
/* include fs90setup.dcsh */

/* constraints - tailor to your own technology. An average 0.35 std-cell tech
   should give you 80 - 100 MHz depending on ram access time. A 0.25 tech
   should run at +150 MHz. Frequency in MHz, delays in ns ...  */

frequency = 166.7
clock_skew = 0.10
input_setup = 2.0
output_delay = 4.0

/* don't touch anything from here unless you know what you are doing */

hdlin_ff_always_sync_set_reset = true
hdlin_translate_off_skip_text = true

sh "rm -rf WORK"
sh "mkdir WORK"
define_design_lib WORK -path WORK
analyze -f verilog ../../leon/ethermac.v
analyze -f VHDL -library WORK ../../leon/amba.vhd
analyze -f VHDL -library WORK ../../leon/target.vhd
analyze -f VHDL -library WORK ../../leon/device.vhd
analyze -f VHDL -library WORK ../../leon/config.vhd
analyze -f VHDL -library WORK ../../leon/sparcv8.vhd
analyze -f VHDL -library WORK ../../leon/mmuconfig.vhd
analyze -f VHDL -library WORK ../../leon/iface.vhd
analyze -f VHDL -library WORK ../../leon/macro.vhd
analyze -f VHDL -library WORK ../../leon/bprom.vhd
analyze -f VHDL -library WORK ../../leon/multlib.vhd
analyze -f VHDL -library WORK ../../leon/tech_generic.vhd
analyze -f VHDL -library WORK ../../leon/tech_virtex.vhd
analyze -f VHDL -library WORK ../../leon/tech_virtex2.vhd
analyze -f VHDL -library WORK ../../leon/tech_atc18.vhd
analyze -f VHDL -library WORK ../../leon/tech_atc25.vhd
analyze -f VHDL -library WORK ../../leon/tech_atc35.vhd
analyze -f VHDL -library WORK ../../leon/tech_fs90.vhd
analyze -f VHDL -library WORK ../../leon/tech_tsmc25.vhd
analyze -f VHDL -library WORK ../../leon/tech_umc18.vhd
analyze -f VHDL -library WORK ../../leon/tech_proasic.vhd
analyze -f VHDL -library WORK ../../leon/tech_axcel.vhd
analyze -f VHDL -library WORK ../../leon/tech_map.vhd
analyze -f VHDL -library WORK ../../leon/mmu_icache.vhd
analyze -f VHDL -library WORK ../../leon/mmu_dcache.vhd
analyze -f VHDL -library WORK ../../leon/mmu_acache.vhd
analyze -f VHDL -library WORK ../../leon/mmutlbcam.vhd
analyze -f VHDL -library WORK ../../leon/mmulrue.vhd
analyze -f VHDL -library WORK ../../leon/mmulru.vhd
analyze -f VHDL -library WORK ../../leon/mmutlb.vhd
analyze -f VHDL -library WORK ../../leon/mmutw.vhd
analyze -f VHDL -library WORK ../../leon/mmu.vhd
analyze -f VHDL -library WORK ../../leon/mmu_cache.vhd
analyze -f VHDL -library WORK ../../leon/cachemem.vhd
analyze -f VHDL -library WORK ../../leon/icache.vhd
analyze -f VHDL -library WORK ../../leon/dcache.vhd
analyze -f VHDL -library WORK ../../leon/acache.vhd
analyze -f VHDL -library WORK ../../leon/cache.vhd
analyze -f VHDL -library WORK ../../leon/ambacomp.vhd
analyze -f VHDL -library WORK ../../leon/apbmst.vhd
analyze -f VHDL -library WORK ../../leon/ahbmst.vhd
analyze -f VHDL -library WORK ../../leon/ahbstat.vhd
analyze -f VHDL -library WORK ../../leon/ahbtest.vhd
analyze -f VHDL -library WORK ../../leon/ahbram.vhd
analyze -f VHDL -library WORK ../../leon/ahbarb.vhd
analyze -f VHDL -library WORK ../../leon/lconf.vhd
analyze -f VHDL -library WORK ../../leon/fpulib.vhd
analyze -f VHDL -library WORK ../../leon/fpu_lth.vhd
analyze -f VHDL -library WORK ../../leon/meiko.vhd
analyze -f VHDL -library WORK ../../leon/fpu_core.vhd
/* analyze -f VHDL -library WORK ../../leon/fp1eu.vhd */
analyze -f VHDL -library WORK ../../leon/ioport.vhd
analyze -f VHDL -library WORK ../../leon/irqctrl.vhd
analyze -f VHDL -library WORK ../../leon/sdmctrl.vhd
analyze -f VHDL -library WORK ../../leon/mctrl.vhd
analyze -f VHDL -library WORK ../../leon/rstgen.vhd
analyze -f VHDL -library WORK ../../leon/timers.vhd
analyze -f VHDL -library WORK ../../leon/uart.vhd
analyze -f VHDL -library WORK ../../leon/mul.vhd
analyze -f VHDL -library WORK ../../leon/div.vhd
analyze -f VHDL -library WORK ../../leon/iu.vhd
analyze -f VHDL -library WORK ../../leon/dcom_uart.vhd
analyze -f VHDL -library WORK ../../leon/dcom.vhd
analyze -f VHDL -library WORK ../../leon/dsu_mem.vhd
analyze -f VHDL -library WORK ../../leon/dsu.vhd
analyze -f VHDL -library WORK ../../leon/proc.vhd
analyze -f VHDL -library WORK ../../leon/wprot.vhd
analyze -f VHDL -library WORK ../../leon/pci_gr.vhd
analyze -f VHDL -library WORK ../../leon/pci.vhd
analyze -f VHDL -library WORK ../../leon/mcore.vhd
analyze -f VHDL -library WORK ../../leon/leon_eth_pci.vhd

elaborate leon_eth_pci
current_design leon_eth_pci
uniquify

ungroup find(cell, "*pad*") -flatten

current_instance mcore0
group find(cell, {"wp*", "asm*", "apb*", "uart*", "timer*", "irq*", "iopo*" \
  "ahb*", "mctrl*", "lc*", "reset*", "dcom*" })  -design_name amod \
  -cell_name amod0

current_instance amod0
ungroup -all -flatten 
/*
current_instance ../dsu0
ungroup -all -flatten 
current_instance ../dsum0
ungroup -all -flatten 
current_instance ../fpu0
ungroup -all -flatten 
*/
current_instance ../proc0/iu0
ungroup -all -flatten
current_instance ../rf0
ungroup -all -flatten 
current_instance ../c0
ungroup -all -flatten 
current_instance ../cmem0
ungroup -all -flatten 
current_instance ../../..

peri = 1000.0 / frequency
input_delay = peri - input_setup
tdelay = output_delay + 2
create_clock -name "clk" -period peri -waveform { 0.0, peri / 2.0 } { "clk" } 
create_clock -name "pci_clk_in" -period peri -waveform { 0.0, 15.0 } { "pci_clk_in" } 
create_clock -name "etx_clk" -period peri -waveform { 0.0, 15.0 } { "etx_clk" } 
create_clock -name "erx_clk" -period peri -waveform { 0.0, 15.0 } { "erx_clk" } 
set_wire_load_mode segmented

set_clock_skew -plus_uncertainty clock_skew "clk"
set_clock_skew -minus_uncertainty clock_skew "clk"
set_clock_skew -plus_uncertainty clock_skew "pci_clk_in"
set_clock_skew -minus_uncertainty clock_skew "pci_clk_in"

set_max_delay 10 -to { \
	 "pci_frame_n" \
	 "pci_irdy_n" "pci_trdy_n" "pci_devsel_n" "pci_stop_n" \
	 "pci_perr_n" "pci_par" "pci_req_n" "pci_serr_n" \
	 "pci_cbe_n[3]" "pci_cbe_n[2]" "pci_cbe_n[1]" "pci_cbe_n[0]" \
	 "pci_ad[31]" "pci_ad[30]" "pci_ad[29]" "pci_ad[28]" "pci_ad[27]" "pci_ad[26]" \
	 "pci_ad[25]" "pci_ad[24]" "pci_ad[23]" "pci_ad[22]" "pci_ad[21]" "pci_ad[20]" \
	 "pci_ad[19]" "pci_ad[18]" "pci_ad[17]" "pci_ad[16]" "pci_ad[15]" "pci_ad[14]" \
	 "pci_ad[13]" "pci_ad[12]" "pci_ad[11]" "pci_ad[10]" "pci_ad[9]" "pci_ad[8]" \
	 "pci_ad[7]" "pci_ad[6]" "pci_ad[5]" "pci_ad[4]" "pci_ad[3]" "pci_ad[2]" \
	 "pci_ad[1]" "pci_ad[0]" }

set_input_delay 6 -clock pci_clk_in { \
	 "pci_gnt_in_n" "pci_idsel_in" "pci_lock_n" "pci_frame_n" \
	 "pci_irdy_n" "pci_trdy_n" "pci_devsel_n" "pci_stop_n" \
	 "pci_perr_n" "pci_par" "pci_req_n" "pci_serr_n" \
	 "pci_cbe_n[3]" "pci_cbe_n[2]" "pci_cbe_n[1]" "pci_cbe_n[0]" \
	 "pci_ad[31]" "pci_ad[30]" "pci_ad[29]" "pci_ad[28]" "pci_ad[27]" "pci_ad[26]" \
	 "pci_ad[25]" "pci_ad[24]" "pci_ad[23]" "pci_ad[22]" "pci_ad[21]" "pci_ad[20]" \
	 "pci_ad[19]" "pci_ad[18]" "pci_ad[17]" "pci_ad[16]" "pci_ad[15]" "pci_ad[14]" \
	 "pci_ad[13]" "pci_ad[12]" "pci_ad[11]" "pci_ad[10]" "pci_ad[9]" "pci_ad[8]" \
	 "pci_ad[7]" "pci_ad[6]" "pci_ad[5]" "pci_ad[4]" "pci_ad[3]" "pci_ad[2]" \
	 "pci_ad[1]" "pci_ad[0]" }

set_input_delay input_delay -clock clk { \
	 "pio[15]" "pio[14]" "pio[13]" \
	 "pio[12]" "pio[11]" "pio[10]" "pio[9]" "pio[8]" "pio[7]" \
	 "pio[6]" "pio[5]" "pio[4]" "pio[3]" "pio[2]" "pio[1]" "pio[0]" \
	 "data[31]" "data[30]" "data[29]" "data[28]" "data[27]" "data[26]" \
	 "data[25]" "data[24]" "data[23]" "data[22]" "data[21]" "data[20]" \
	 "data[19]" "data[18]" "data[17]" "data[16]" "data[15]" "data[14]" \
	 "data[13]" "data[12]" "data[11]" "data[10]" "data[9]" "data[8]" \
	 "data[7]" "data[6]" "data[5]" "data[4]" "data[3]" "data[2]" \
	 "data[1]" "data[0]" "brdyn" "bexcn" }

set_max_delay tdelay -to { "errorn" "wdogn" \
         "pio[15]" "pio[14]" "pio[13]" \
	 "pio[12]" "pio[11]" "pio[10]" "pio[9]" "pio[8]" "pio[7]" \
	 "pio[6]" "pio[5]" "pio[4]" "pio[3]" "pio[2]" "pio[1]" "pio[0]" \
	 "data[31]" "data[30]" "data[29]" "data[28]" "data[27]" "data[26]" \
	 "data[25]" "data[24]" "data[23]" "data[22]" "data[21]" "data[20]" \
	 "data[19]" "data[18]" "data[17]" "data[16]" "data[15]" "data[14]" \
	 "data[13]" "data[12]" "data[11]" "data[10]" "data[9]" "data[8]" \
	 "data[7]" "data[6]" "data[5]" "data[4]" "data[3]" "data[2]" \
	 "data[1]" "data[0]" }

set_max_delay output_delay -to { \
	 "writen" "romsn[1]" "romsn[0]" "read" "oen" \
	 "iosn" "rwen[3]" "rwen[2]" "rwen[1]" "rwen[0]" "ramsn[3]" \
	 "ramsn[2]" "ramsn[1]" "ramsn[0]" \
	 "ramoen[3]" "ramoen[2]" "ramoen[1]" "ramoen[0]" \
	 "sdcsn[1]" "sdcsn[0]" "sdwen" "sdrasn" "sdcasn" \
	 "sddqm[3]" "sddqm[2]" "sddqm[1]" "sddqm[0]" \
	 "address[27]" "address[26]" "address[25]" "address[24]" \
	 "address[23]" "address[22]" "address[21]" "address[20]" \
	 "address[19]" "address[18]" "address[17]" "address[16]" \
	 "address[15]" "address[14]" "address[13]" "address[12]" \
	 "address[11]" "address[10]" "address[9]" "address[8]" \
	 "address[7]" "address[6]" "address[5]" "address[4]" \
	 "address[3]" "address[2]" "address[1]" "address[0]"}

set_max_area 0  
set_max_transition 2.0 leon_eth_pci  
set_flatten false -design {"leon_eth_pci.db:leon_eth_pci"} 
set_structure true -design {"leon_eth_pci.db:leon_eth_pci"} -boolean false -timing true

/* include fs90cond.dcsh */
/* include atc25cond.dcsh */
include atc18cond.dcsh

compile -map_effort medium -boundary_optimization

write -f db -hier leon_eth_pci -output leon_eth_pci.db

report_timing

current_design mcore
report_area
current_design leon_eth_pci
